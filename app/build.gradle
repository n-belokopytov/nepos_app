apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.nepos.webapp"
        minSdkVersion 25
        targetSdkVersion 29
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("../nepos.keystore")
            storePassword ANDROID_STORE_PASSWORD
            keyAlias ANDROID_KEY_ALIAS
            keyPassword ANDROID_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "api", "usergroup"

    productFlavors {
        prod {
            dimension "api"
            resValue "string", "app_name", "Nepos"
            resValue "string", "api_url", "nepos.app"
        }

        stage {
            dimension "api"
            applicationIdSuffix ".stage"
            resValue "string", "app_name", "Nepos-Stage"
            resValue "string", "api_url", "stage.nepos.tech"
            versionNameSuffix "-stage"
            versionCode 1000000 + android.defaultConfig.versionCode
        }

        dev {
            dimension "api"
            resValue "string", "app_name", "Nepos-Dev"
            resValue "string", "api_url", "dev.nepos.tech"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            versionCode 2000000 + android.defaultConfig.versionCode
        }

        generic {
            dimension "usergroup"
            resValue "string", "usergroup", ""
        }

        blz {
            dimension "usergroup"
            resValue "string", "usergroup", "berliner-zeitung"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
}
